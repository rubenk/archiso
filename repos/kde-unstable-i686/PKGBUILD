# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-applets-plasma-nm
pkgver=677.f4cf239
pkgrel=1
pkgdesc='Plasma applet written in QML for managing network connections'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/network/plasma-nm'
license=('GPL2')
depends=('kdebase-workspace' 'libnm-qt' 'openconnect')
makedepends=('cmake' 'automoc4' 'git')
source=('git://anongit.kde.org/plasma-nm.git')
md5sums=('SKIP')

pkgver() {
  cd plasma-nm
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../plasma-nm \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
