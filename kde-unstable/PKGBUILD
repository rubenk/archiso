# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt
pkgver=657646b
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4')
makedepends=('cmake' 'git')
source=('git://anongit.kde.org/libmm-qt')
md5sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --always | sed 's|-|.|g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=${pkgdir} install
}
